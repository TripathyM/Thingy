plugins {
    id 'org.springframework.boot' version '2.1.4.RELEASE'
    id 'java'
    id 'com.palantir.docker' version '0.22.1'
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.palantir.docker'

group = 'com.todo'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

bootJar {
    baseName = 'thingy'
    version = '0.1.0'
}


configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation ('org.springframework.boot:spring-boot-starter-test') {
        exclude group: "junit"
    }
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.2'
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '2.23.4'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.12.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.4.2'
}

test {
    useJUnitPlatform {
        includeEngines 'junit-jupiter'
    }
}

task unpack(type: Copy) {
    dependsOn bootJar
    from(zipTree(tasks.bootJar.outputs.files.singleFile))
    into("build/dependency")
}

docker {
    name "${project.group}/${bootJar.baseName}"
    copySpec.from(tasks.unpack.outputs).into("dependency")
    buildArgs(['DEPENDENCY': "dependency"])
}
